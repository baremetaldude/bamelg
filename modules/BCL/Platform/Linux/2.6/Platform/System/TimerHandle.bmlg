//
// Created by Eugeny Grishul
//
// See license at http://bamelg.com/license.txt
//

using System;

using Platform.Libc;

namespace Platform.Kernel {
	public struct TimerHandle : IOHandle {
		public static new thistype Invalid { get { return bitcast<thistype>( basetype.Invalid ); } }

		public TimerHandle( ClockType clockType ) {
			LibcApi.Assert( this = LibcApi.timerfd_create( clockType, TimerHandleCreateFlag.TFD_NONBLOCK | TimerHandleCreateFlag.TFD_CLOEXEC ) );
		}

		public void SetInterval( TimeSpan interval ) { SetInterval( interval, interval ); }

		public void SetInterval( TimeSpan interval, TimeSpan initialExpiration ) {
			var timer = new itimerspec {
				it_interval = ( timespec ) interval,
				it_value = ( timespec ) initialExpiration
			};

			LibcApi.AssertErrno( LibcApi.timerfd_settime( this, TimerHandleSetFlag.None, &timer, null ) );
		}

		// initialExpiration value depends on clockid
		public void SetInterval( TimeSpan interval, DateTime initialExpiration ) {
			var timer = new itimerspec {
				it_interval = ( timespec ) interval,
				it_value = ( timespec ) initialExpiration
			};

			LibcApi.AssertErrno( LibcApi.timerfd_settime( this, TimerHandleSetFlag.TFD_TIMER_ABSTIME, &timer, null ) );
		}

		public void Stop() {
			var stop = default( itimerspec );
			LibcApi.AssertErrno( LibcApi.timerfd_settime( this, TimerHandleSetFlag.TFD_TIMER_ABSTIME, &stop, null ) );
		}

		public itimerspec GetInterval() {
			itimerspec result;
			LibcApi.AssertErrno( LibcApi.timerfd_gettime( this, &result ) );
			return result;
		}

		// don't implement it via property to prevent evaluation in debugger
		public long ReadExpirations() {
			long result;
			LibcApi.read( this, &result, sizeof( result ) );
			return result;
		}
	}
}